extends CharacterBody2D

enum playerstate {
	idle,
	walk,
	jump,
	fall,
	duck
}

@onready var anim: AnimatedSprite2D = $AnimatedSprite2D
@onready var collision_shape: CollisionShape2D = $CollisionShape2D

const SPEED = 80.0

# Quanto menor, mais alto ele pula
const JUMP_VELOCITY = -300.0

var jump_count = 0
@export var max_jump_count = 2
var direction = 0
var status: playerstate

func _ready() -> void:
	go_to_idle_state()

func _physics_process(delta: float) -> void:
	
	if not is_on_floor():
		velocity += get_gravity() * delta
	
	match status:
		playerstate.idle:
			idle_state()
		playerstate.walk:
			walk_state()
		playerstate.jump:
			jump_state()
		playerstate.fall:
			fall_state()
		playerstate.duck:
			duck_state()
			
	move_and_slide()

func go_to_idle_state():
	status = playerstate.idle
	anim.play("idle")

func go_to_walk_state():
	status = playerstate.walk
	anim.play("walk")

func go_to_jump_state():
	status = playerstate.jump
	anim.play("jump")
	velocity.y = JUMP_VELOCITY
	jump_count += 1
	
func go_to_fall_state():
	status = playerstate.fall
	anim.play("fall")
	
func go_to_duck_state():
	status = playerstate.duck
	anim.play("duck")
	collision_shape.shape.radius = 5.0
	collision_shape.shape.height = 25.0
	collision_shape.position.x = 2
	collision_shape.position.y = 3
	
func exit_from_duck_state():
	collision_shape.shape.radius = 5.0
	collision_shape.shape.height = 35.0
	collision_shape.position.x = 1
	collision_shape.position.y = 1
	
func idle_state():
	move()
	if velocity.x != 0:
		go_to_walk_state()
		return
		
	if Input.is_action_just_pressed("jump"):
		go_to_jump_state()
		return
		
	if Input.is_action_pressed("duck"):
		go_to_duck_state()
		return
	
func walk_state():
	move()
	if velocity.x ==0:
		go_to_idle_state()
		return
		
	if Input.is_action_just_pressed("jump"):
		go_to_jump_state()
		return
		
	if !is_on_floor():
		jump_count += 1
		go_to_fall_state()
		return
		
	
func jump_state():
	move()
	
	if Input.is_action_just_pressed("jump") && jump_count < max_jump_count:
		go_to_jump_state()
		return
	
	if velocity.y > 0:
		go_to_fall_state()
		return
		
func fall_state():
	move()
	
	if Input.is_action_just_pressed("jump") && jump_count < max_jump_count:
		go_to_jump_state()
		return
	
	if is_on_floor():
		jump_count = 0
		if velocity.x == 0:
			go_to_idle_state()
		else:
			go_to_walk_state()
		return
		
func duck_state():
	update_direction()
	if Input.is_action_just_released("duck"):
		exit_from_duck_state()
		go_to_idle_state()
		return

func move():
	update_direction()
	
	if direction:
		velocity.x = direction * SPEED
	else:
		velocity.x = move_toward(velocity.x, 0, SPEED)

func update_direction():
	direction = Input.get_axis("left", "right")	
	if direction < 0:
		anim.flip_h = true
	elif direction > 0:
		anim.flip_h = false

  
